---
author: zjmantou
title: 03.Lazy Configuration
time: 2024-07-31 周三
tags:
  - 技术
  - Gradle
---
# [堕性属性](https://docs.gradle.org/7.6/userguide/lazy_configuration.html) 

## Provider 

只能查询，不能更改 

- 只读；
- Provider.get();
- Provider.map(Transformer);
- 许多其他类型扩展了`Provider` ，并且可以在需要`Provider`任何地方使用。

## Property 

可以查询和更改 

- 继承自Provider
- Property.set(Provider)
- 可以通过工厂方法ObjectFactory.property(Class)创建Property 

```Groovy
//build.gradle

// A task that displays a greeting
abstract class Greeting extends DefaultTask {
    // A configurable greeting
    @Input
    abstract Property<String> getGreeting()

    // Read-only property calculated from the greeting
    @Internal
    final Provider<String> message = greeting.map { it + ' from Gradle' }

    @TaskAction
    void printMessage() {
        logger.quiet(message.get())
    }
}

tasks.register("greeting", Greeting) {
    // Configure the greeting
    greeting.set('Hi')
    greeting = 'Hi' // Alternative notation to calling Property.set() - only available in Groovy DSL
}

//输出
$ gradle greeting

> Task :greeting
Hi from Gradle

BUILD SUCCESSFUL in 0s
1 actionable task: 1 executed
```

# 创建 Property 或 Provider 实例 

`Provider`及其子类型（例如`Property`都不适合由构建脚本或插件作者实现。 Gradle 提供了工厂方法来创建这些类型的实例。请参阅[快速参考](https://docs.gradle.org/7.6/userguide/lazy_configuration.html#lazy_configuration_reference)了解所有可用的类型和工厂。在前面的示例中，我们看到了 2 个工厂方法：
- [ObjectFactory.property(Class)](https://docs.gradle.org/7.6/javadoc/org/gradle/api/model/ObjectFactory.html#property-java.lang.Class-)创建一个新的`Property`实例。可以从[Project.getObjects()](https://docs.gradle.org/7.6/javadoc/org/gradle/api/Project.html#getObjects--)引用[ObjectFactory](https://docs.gradle.org/7.6/javadoc/org/gradle/api/model/ObjectFactory.html)的实例，也可以通过构造函数或方法注入`ObjectFactory`来引用。
- [Provider.map(Transformer)](https://docs.gradle.org/7.6/javadoc/org/gradle/api/provider/Provider.html#map-org.gradle.api.Transformer-)从现有的`Provider`或`Property`实例创建一个新的`Provider` 

# 连接属性 

它们可以连接在一起，以便对一个属性的更改自动反映在其他属性中。 

```Groovy
// A project extension
interface MessageExtension {
    // A configurable greeting
    Property<String> getGreeting()
}

// A task that displays a greeting
abstract class Greeting extends DefaultTask {
    // Configurable by the user
    @Input
    abstract Property<String> getGreeting()

    // Read-only property calculated from the greeting
    @Internal
    final Provider<String> message = greeting.map { it + ' from Gradle' }

    @TaskAction
    void printMessage() {
        logger.quiet(message.get())
    }
}

// Create the project extension
project.extensions.create('messages', MessageExtension)

// Create the greeting task
tasks.register("greeting", Greeting) {
    // Attach the greeting from the project extension
    // Note that the values of the project extension have not been configured yet
    greeting = messages.greeting
}

messages {
    // Configure the greeting on the extension
    // Note that there is no need to reconfigure the task's `greeting` property. This is automatically updated as the extension property changes
    greeting = 'Hi'
}

$ gradle greeting

> Task :greeting
Hi from Gradle

BUILD SUCCESSFUL in 0s
1 actionable task: 1 executed
```

# 处理文件 

| Read-only Type 只读类型                                                                                | Configurable Type 可配置类型                                                                                                            |
| -------------------------------------------------------------------------------------------------- | ---------------------------------------------------------------------------------------------------------------------------------- |
| [FileCollection 文件集合](https://docs.gradle.org/7.6/javadoc/org/gradle/api/file/FileCollection.html) | [ConfigurableFileCollection  <br>可配置文件集合](https://docs.gradle.org/7.6/javadoc/org/gradle/api/file/ConfigurableFileCollection.html) |
| [FileTree 文件树](https://docs.gradle.org/7.6/javadoc/org/gradle/api/file/FileTree.html)              | [ConfigurableFileTree 可配置文件树](https://docs.gradle.org/7.6/javadoc/org/gradle/api/file/ConfigurableFileTree.html)                   |

 [Directory](https://docs.gradle.org/7.6/javadoc/org/gradle/api/file/Directory.html)和[RegularFile](https://docs.gradle.org/7.6/javadoc/org/gradle/api/file/RegularFile.html) 


Property的子类： [RegularFileProperty](https://docs.gradle.org/7.6/javadoc/org/gradle/api/file/RegularFileProperty.html)和[DirectoryProperty](https://docs.gradle.org/7.6/javadoc/org/gradle/api/file/DirectoryProperty.html) 

## RegularFileProperty 

可用于分别通过DirectoryProperty.dir(String)和DirectoryProperty.file(String)为Directory和RegularFile创建延迟评估的Provider。 

```Groovy
// A task that generates a source file and writes the result to an output directory
abstract class GenerateSource extends DefaultTask {
    // The configuration file to use to generate the source file
    @InputFile
    abstract RegularFileProperty getConfigFile()

    // The directory to write source files to
    @OutputDirectory
    abstract DirectoryProperty getOutputDir()

    @TaskAction
    def compile() {
        def inFile = configFile.get().asFile
        logger.quiet("configuration file = $inFile")
        def dir = outputDir.get().asFile
        logger.quiet("output dir = $dir")
        def className = inFile.text.trim()
        def srcFile = new File(dir, "${className}.java")
        srcFile.text = "public class ${className} { ... }"
    }
}

// Create the source generation task
tasks.register('generate', GenerateSource) {
    // Configure the locations, relative to the project and build directories
    configFile = layout.projectDirectory.file('src/config.txt')
    outputDir = layout.buildDirectory.dir('generated-source')
}

// Change the build directory
// Don't need to reconfigure the task properties. These are automatically updated as the build directory changes
layout.buildDirectory = layout.projectDirectory.dir('output')

$ gradle generate

> Task :generate
configuration file = /home/user/gradle/samples/src/config.txt
output dir = /home/user/gradle/samples/output/generated-source

BUILD SUCCESSFUL in 0s
1 actionable task: 1 executed

```

要关闭循环，请注意`DirectoryProperty`或简单的`Directory`可以转换为`FileTree` ，允许使用[DirectoryProperty.getAsFileTree()](https://docs.gradle.org/7.6/javadoc/org/gradle/api/file/DirectoryProperty.html#getAsFileTree--)或[Directory.getAsFileTree()](https://docs.gradle.org/7.6/javadoc/org/gradle/api/file/Directory.html#getAsFileTree--)查询目录中包含的文件和目录。此外，从`DirectoryProperty`或`Directory`中，您还可以使用[DirectoryProperty.files(Object...)](https://docs.gradle.org/7.6/javadoc/org/gradle/api/file/DirectoryProperty.html#files-java.lang.Object...-)或[Directory.files(Object...)](https://docs.gradle.org/7.6/javadoc/org/gradle/api/file/Directory.html#files-java.lang.Object...-)创建包含目录中包含的一组文件的`FileCollection`实例。 

# 使用任务输入和输出

```Groovy
abstract class Producer extends DefaultTask {
    @OutputFile
    abstract RegularFileProperty getOutputFile()

    @TaskAction
    void produce() {
        String message = 'Hello, World!'
        def output = outputFile.get().asFile
        output.text = message
        logger.quiet("Wrote '${message}' to ${output}")
    }
}

abstract class Consumer extends DefaultTask {
    @InputFile
    abstract RegularFileProperty getInputFile()

    @TaskAction
    void consume() {
        def input = inputFile.get().asFile
        def message = input.text
        logger.quiet("Read '${message}' from ${input}")
    }
}

def producer = tasks.register("producer", Producer)
def consumer = tasks.register("consumer", Consumer)

consumer.configure {
    // Connect the producer task output to the consumer task input
    // Don't need to add a task dependency to the consumer task. This is automatically added
    inputFile = producer.flatMap { it.outputFile }
}

producer.configure {
    // Set values for the producer lazily
    // Don't need to update the consumer.inputFile property. This is automatically updated as producer.outputFile changes
    outputFile = layout.buildDirectory.file('file.txt')
}

// Change the build directory.
// Don't need to update producer.outputFile and consumer.inputFile. These are automatically updated as the build directory changes
layout.buildDirectory = layout.projectDirectory.dir('output')

$ gradle consumer

> Task :producer
Wrote 'Hello, World!' to /home/user/gradle/samples/output/file.txt

> Task :consumer
Read 'Hello, World!' from /home/user/gradle/samples/output/file.txt

BUILD SUCCESSFUL in 0s
2 actionable tasks: 2 executed
```

# 使用集合 ListProperty. 

可以使用[ObjectFactory.listProperty(Class)](https://docs.gradle.org/7.6/javadoc/org/gradle/api/model/ObjectFactory.html#listProperty-java.lang.Class-)创建新的`ListProperty`并指定元素类型。 

使用[HasMultipleValues.set(Iterable)](https://docs.gradle.org/7.6/javadoc/org/gradle/api/provider/HasMultipleValues.html#set-java.lang.Iterable-)和[HasMultipleValues.set(Provider)](https://docs.gradle.org/7.6/javadoc/org/gradle/api/provider/HasMultipleValues.html#set-org.gradle.api.provider.Provider-)覆盖整个集合值，或者通过各种`add`方法添加新元素：

- [HasMultipleValues.add(T)](https://docs.gradle.org/7.6/javadoc/org/gradle/api/provider/HasMultipleValues.html#add-T-) : 将单个元素添加到集合中
- [HasMultipleValues.add(Provider)](https://docs.gradle.org/7.6/javadoc/org/gradle/api/provider/HasMultipleValues.html#add-org.gradle.api.provider.Provider-) : 将延迟计算的元素添加到集合中
- [HasMultipleValues.addAll(Provider)](https://docs.gradle.org/7.6/javadoc/org/gradle/api/provider/HasMultipleValues.html#addAll-org.gradle.api.provider.Provider-) ：将延迟计算的元素集合添加到列表中

# 使用Map MapProperty 

ObjectFactory.mapProperty(Class, Class).

# convention 

约定默认值

property.convention("convention 1") 


#  [指南](https://docs.gradle.org/7.6/userguide/lazy_configuration.html#lazy_configuration_faqs)

[gradle-site-plugin](https://github.com/gradle/gradle-site-plugin) 

演示了插件开发的既定技术和实践的插件demo 。

## [API 参考](https://docs.gradle.org/7.6/userguide/lazy_configuration.html#lazy_configuration_reference) 

